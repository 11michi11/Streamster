plugins {
	id 'org.springframework.boot' version '2.2.6.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'com.google.cloud.tools.jib' version '2.6.0'
	id 'java'
	id 'groovy'
}

group = 'com.streamster'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '15'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

def dockerPassword = hasProperty('gcloudToken') ? gcloudToken : file('../keyfile.json').text

jib {
	from {
		image = 'adoptopenjdk/openjdk15:jdk-15.0.1_9-alpine-slim'
	}
	to {
		image = 'gcr.io/streamster-289010/search-service:latest'
		auth {
			username = '_json_key'
			password = dockerPassword
		}
	}
	container{
		jvmFlags = ['-Dspring.profiles.active=prod', '--enable-preview']
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "Hoxton.SR3")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-amqp'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.security.oauth:spring-security-oauth2:2.5.0.RELEASE'
	compile 'io.springfox:springfox-swagger2:2.9.2'
	compile 'io.springfox:springfox-swagger-ui:2.9.2'
	compile "javax.xml.bind:jaxb-api:2.3.0"
	compile "com.sun.xml.bind:jaxb-core:2.3.0"
	compile "com.sun.xml.bind:jaxb-impl:2.3.0"
	compile "javax.activation:activation:1.1.1"
    compileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.amqp:spring-rabbit-test'
	testCompile "org.springframework.security:spring-security-test"
	testCompile "org.codehaus.groovy:groovy:2.5.13"
	testCompile platform("org.spockframework:spock-bom:2.0-M3-groovy-2.5")
	testCompile "org.spockframework:spock-core"
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
	testCompile group: 'org.spockframework', name: 'spock-spring', version: '2.0-M3-groovy-2.5'
	implementation project(":commons")
}

tasks.withType(JavaCompile) {
	options.compilerArgs += '--enable-preview'
}

tasks.withType(Test) {
	jvmArgs += "--enable-preview"
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

test {
	useJUnitPlatform()
}
